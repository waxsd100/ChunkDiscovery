plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'xyz.jpenilla.run-paper' version '2.2.4'
    id 'jacoco'
    id 'org.owasp.dependencycheck' version '9.2.0'
}

group = 'io.wax100'
version = '1.0-SNAPSHOT'
description = 'ChunkDiscoveryPlugin'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    // repositories は settings.gradle で統一管理しているので空でも OK
}

dependencies {
    // Paper API
    compileOnly 'io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT'

    // HikariCP / MySQL ドライバを fatJar に含める
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'mysql:mysql-connector-java:8.0.33'

    // テスト依存関係
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'org.mockito:mockito-core:5.6.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.6.0'
    testImplementation 'com.h2database:h2:2.2.224'
    testImplementation 'io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT'
    
    // Testcontainers
    testImplementation 'org.testcontainers:junit-jupiter:1.19.3'
    testImplementation 'org.testcontainers:mysql:1.19.3'
    
    // JUnit Platform (明示的に指定してテストフレームワーク警告を回避)
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = 17
}

// テスト設定
test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
    maxHeapSize = "1G"
}

// ShadowJar 設定
shadowJar {
    archiveClassifier = ''
    // 依存関係をリロケート
    relocate 'com.zaxxer.hikari', 'io.wax100.chunkdiscovery.lib.hikari'
    relocate 'org.slf4j', 'io.wax100.chunkdiscovery.lib.slf4j'
    relocate 'com.mysql', 'io.wax100.chunkdiscovery.lib.mysql'

    // 除外するファイル（不要なメタデータ）
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/LICENSE*'
    exclude 'META-INF/NOTICE*'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'

    // JARファイル名の最終設定
    archiveBaseName = 'chunk-discovery-plugin'
    archiveVersion = project.version.toString()
}

// デフォルトでshadowJarを実行
build.dependsOn shadowJar

processResources {
    filesMatching('plugin.yml') {
        expand(version: project.version, apiVersion: '1.20')
    }
}

// Jacoco設定
jacoco {
    toolVersion = '0.8.10'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.35
            }
        }
    }
}

// OWASP Dependency Check設定
dependencyCheck {
    formats = ['HTML']
    failBuildOnCVSS = 9.0
    skipConfigurations = ['testImplementation', 'testRuntimeOnly', 'testCompileClasspath', 'testRuntimeClasspath']
}